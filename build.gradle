plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'application'
    id 'java-library'
    id 'jacoco'
    id 'org.sonarqube' version '3.1.1'
}

ext {
    hapiVersion = '5.3.0'
}

allprojects {
    repositories {
        mavenCentral()
    }

    group = 'gov.usds.vaccineschedule'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    test {
        environment "SPRING_PROFILES_ACTIVE", ""
        useJUnitPlatform()
    }
}


subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    jacocoTestReport {
        reports {
            xml.enabled = true
        }
        dependsOn test // tests are required to run before generating the report
    }
}

task stage {
    dependsOn(['clean', ':api-application:assemble'])
    assemble.mustRunAfter clean
    doLast {
        delete (fileTree(dir: "api-application/build", exclude: "libs"))
        delete (fileTree(dir: "api-application/build/libs", exclude: "*.jar"))

        delete (fileTree(dir: "publisher-application/build", exclude: "libs"))
        delete (fileTree(dir: "publisher-application/build/libs", exclude: "*.jar"))

        delete (fileTree(dir: "common/build", exclude: "libs"))
        delete (fileTree(dir: "common/build/libs", exclude: "*.jar"))
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "nickrobison-usds_vaccine-scheduler"
        property "sonar.organization", "nickrobison-usds"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}


